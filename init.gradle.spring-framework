allprojects {
  configurations.all {
    resolutionStrategy.dependencySubstitution {
      all { dependency ->
        if (dependency.requested instanceof ModuleComponentSelector) {
          def group = dependency.requested.group
          if (group.startsWith("org.eclipse.jetty")) { // && !group.equals('org.eclipse.jetty.websocket')) {
            def module = dependency.requested.module
            def version = (module == 'jetty-reactive-httpclient') ? System.getProperty("jetty.reactive.httpclient","4.0.9") : System.getProperty("jetty.version","12.0.17")
            dependency.useTarget "$group:$module:$version"
          }
        }
      }
    }
  }
  repositories {
    maven {
      url "https://oss.sonatype.org/content/groups/jetty-with-staging/"
      content {
        includeGroupByRegex("org.eclipse.jetty.*")
      }
    }
    maven {
      url "https://oss.sonatype.org/content/repositories/jetty-snapshots/"
      content {
        includeGroupByRegex("org.eclipse.jetty.*")
      }
    }

  }
}